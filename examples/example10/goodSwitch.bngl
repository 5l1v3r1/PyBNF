begin model

begin parameters

f .1 # dimensionless, 0<f<=1

NA 6.02214e23 # molecules per mol

# cytosolic volume
Vcyt 2e-12*f # L (dm^3) per cell

# protein abundances (copies per cell) 
AMPK_cpc      6.25e4*f # 10 nM
MTORC1_cpc    2.5e4*f #  1 nM
Ragulator_cpc 1.2e5*f # Ragulator_tot > MTORC1_tot
ULK1_cpc      5.6e4*f
EIF4EBP1_cpc  8.8e5*f
AMBRA1_cpc    2e3*f

AMPK_conc=AMPK_cpc/(NA*Vcyt)
MTORC1_conc=MTORC1_cpc/(NA*Vcyt)
Ragulator_conc=Ragulator_cpc/(NA*Vcyt)
ULK1_conc=ULK1_cpc/(NA*Vcyt)
EIF4EBP1_conc=EIF4EBP1_cpc/(NA*Vcyt)
AMBRA1_conc=AMBRA1_cpc/(NA*Vcyt)

k_energy_stress 1 # /s
k_energy_supply 1 # /s

k_nutrient_stress 1 # /s
k_nutrient_supply 1 # /s

# MTORC1 binds Rag-Ragulator complex
kf_MTORC1_RagCGDP 2e6/(NA*Vcyt) # /M/s
kr_MTORC1_RagCGDP 0.002 # /s (KD = 1 nM)

# MTORC1 binds 4EBP1 and phosphorylates 4EBP1
kr_MTORC1_EIF4EBP1 0.5 # /s
kcat_MTORC1_EIF4EBP1 1 # /s (kcat >> kr)
kf_MTORC1_EIF4EBP1=1e7/(NA*Vcyt)
k_dephos_pEIF4EBP1=0.0021 #(-1+1/max_frac_pEIF4EBP1)*kcat_MTORC1_EIF4EBP1*MTORC1_conc/KD_MTORC1_EIF4EBP1

# MTORC1 binds ULK1 at S317 and phosphorylates ULK1 at S758
kr_MTORC1_ULK1 0.2 # /s
kcat_MTORC1_ULK1 10 # /s (kcat >> kr)
kf_MTORC1_ULK1 2.4e8/NA/Vcyt #(kcat_MTORC1_ULK1/KD_MTORC1_ULK1)/(NA*Vcyt)
k_dephos_ULK1_pS758 0.01 # (-1+1/max_frac_ULK1_pS758)*kcat_MTORC1_ULK1*MTORC1_conc/KD_MTORC1_ULK1

# AMPK binds ULK1 at S758 and phosphorylates ULK1 at S317 and S556
kr_AMPK_ULK1 .01 # /s
kf_AMPK_ULK1= 4e5/NA/Vcyt #(kr_AMPK_ULK1/KD_AMPK_ULK1)/(NA*Vcyt)
kcat_AMPK_ULK1 1 # /s (kcat >> kr)
k_dephos_ULK1_pS317= 2 #(-1+1/max_frac_ULK1_pS317)*kcat_AMPK_ULK1*AMPK_conc/KD_AMPK_ULK1
k_dephos_ULK1_pS556= 0.0524 #(-1+1/max_frac_ULK1_pS556)*kcat_AMPK_ULK1*AMPK_conc/KD_AMPK_ULK1

# AMPK phosphorylates MTORC1 at S792 in RAPTOR via Michaelis-Menten mechanism
km1_AMPK_MTORC1 0.1 # /s
kcat_AMPK_MTORC1 1 # /s
kp1_AMPK_MTORC1=0 #1.1e8/NA/Vcyt #((kcat_AMPK_MTORC1+km1_AMPK_MTORC1)/KM_AMPK_MTORC1)/(NA*Vcyt)

# ULK1 phosphorylates AMBRA1 via Michaelis-Menten mechanism
km1_ULK1_AMBRA1 0.01 # /s
kcat_ULK1_AMBRA1 50 # /s 
kp1_ULK1_AMBRA1 2.5e8/NA/Vcyt   #((kcat_ULK1_AMBRA1+km1_ULK1_AMBRA1)/KM_ULK1_AMBRA1)/(NA*Vcyt)
k_dephos_pAMBRA1 0.2 #(-1+1/max_frac_pAMBRA1)*kcat_ULK1_AMBRA1*ULK1_conc/KM_ULK1_AMBRA1

# ULK1 phosphorylates AMPK via Michaelis-Menten mechanism
km1_ULK1_AMPK 0.02 # /s
kcat_ULK1_AMPK 0.2 # /s 
kp1_ULK1_AMPK= 0 #2.2e6/NA/Vcyt #((kcat_ULK1_AMPK+km1_ULK1_AMPK)/KM_ULK1_AMPK)/(NA*Vcyt)
k_dephos_AMPK_pDock= 9.3929e-4 #(-1+1/max_frac_AMPK_pDock)*kcat_ULK1_AMPK*ULK1_conc/KM_ULK1_AMPK

# ULK1 phosphorylates RAPTOR via Michaelis-Menten mechanism
km1_ULK1_MTORC1 0.02 # /s
kcat_ULK1_MTORC1 2.5 # /s 
kp1_ULK1_MTORC1= 8e8/NA/Vcyt #((kcat_ULK1_MTORC1+km1_ULK1_MTORC1)/KM_ULK1_MTORC1)/(NA*Vcyt)
k_dephos_MTORC1_pS855= 0.35 #(-1+1/max_frac_MTORC1_pS855)*kcat_ULK1_MTORC1*ULK1_conc/KM_ULK1_MTORC1
k_dephos_MTORC1_pS859= k_dephos_MTORC1_pS855 #(-1+1/max_frac_MTORC1_pS855)*kcat_ULK1_MTORC1*ULK1_conc/KM_ULK1_MTORC1
k_dephos_MTORC1_pS792= k_dephos_MTORC1_pS855 #(-1+1/max_frac_MTORC1_pS792)*kcat_AMPK_MTORC1*AMPK_conc/KM_AMPK_MTORC1

end parameters

begin molecule types

AMPK(kin,T172~0~P,Dock~0~P)
MTORC1(RNC,ARM,S792~0~P,S855~0~P,S859~0~P)
Ragulator(RagC~GDP~GTP)
ULK1(TOS,kin,S317~0~P,S556~0~P,S758~0~P)
EIF4EBP1(S65_T70~0~P)
AMBRA1(S465_S635~0~P)

end molecule types

begin seed species

# nutient-rich initial condition
AMPK(kin,T172~0,Dock~0) AMPK_cpc
MTORC1(RNC,ARM!1,S792~0,S855~0,S859~0).Ragulator(RagC~GDP!1) MTORC1_cpc
Ragulator(RagC~GDP) Ragulator_cpc-MTORC1_cpc
ULK1(TOS,kin,S317~0,S556~0,S758~P) ULK1_cpc
EIF4EBP1(S65_T70~P) EIF4EBP1_cpc
AMBRA1(S465_S635~0) AMBRA1_cpc

end seed species

begin observables

#Molecules pAMPK_ULK1 AMPK(T172~P,Dock!1).ULK1(S758~0!1)
#Molecules MTORC1_ULK1 MTORC1(RNC!1).ULK1(TOS!1)
#Molecules activeMTORC1_ULK1 MTORC1(RNC!1,ARM!+).ULK1(TOS!1)
#Molecules freeMTORC1 MTORC1(RNC)

#Molecules freeS758 ULK1(S758~0!?)
#Molecules pS758 ULK1(S758~P)

#Molecules freeS317 ULK1(S317~0)
Molecules pS317 ULK1(S317~P)
Molecules pAMBRA1 AMBRA1(S465_S635~P)
Molecules freeS855 MTORC1(S855~0,S859~0,S792~0)
Molecules dissociative MTORC1(S855~P,S859~P,S792~P)


#Molecules a51 AMPK(Dock~0)
#Molecules a52 ULK1(S758~0)
#Molecules a6 AMPK(Dock~0!1).ULK1(S758~0!1)
#Molecules p11 MTORC1(RNC!1,ARM!+).ULK1(TOS!1,S758~0)
#Molecules boundS758 ULK1(S758~0!+)

#Molecules freeS317 ULK1(S317~0)
#Molecules p317 ULK1(S317~P)
#Molecules pp317 ULK1(S317~PP)
#Molecules bound317 ULK1(S317~0!+)



#Species ULK1_dimers ULK1(dim!1).ULK1(dim!1)

end observables

begin functions

end functions

begin reaction rules

# simple model for regulation of AMPK by energy (AMP)
#  [AMP(T172~0)]=AMPK_cpc*k_energy_supply/(k_energy_stress+k_energy_supply)
AMPK(T172~0)->AMPK(T172~P) k_energy_stress
AMPK(T172~P)->AMPK(T172~0) k_energy_supply

# simple model for regulation of MTORC1 by nutrients (amino acids and growth factors)
#  [Ragulator(RagC~GDP)]=Ragulator_cpc*k_nutrient_supply/(k_nutrient_stress+k_nutrient_supply)
Ragulator(RagC~GDP)->Ragulator(RagC~GTP) k_nutrient_stress
Ragulator(RagC~GTP)->Ragulator(RagC~GDP) k_nutrient_supply

# recruitment of MTORC1 to Rag-Ragulator complex on lysosomes
MTORC1(ARM)+Ragulator(RagC~GDP)->\
MTORC1(ARM!1).Ragulator(RagC~GDP!1) kf_MTORC1_RagCGDP
#  dissocation 
MTORC1(ARM!1).Ragulator(RagC~GDP!1)->\
MTORC1(ARM)+Ragulator(RagC~GDP) kr_MTORC1_RagCGDP

# phosphorylation of ULK1 by MTORC1 via Michaelis-Menten-like mechanism
#  We assume that MTORC1 binding to ULK1 shields S317 from phosphorylation by AMPK.
#  NB: both MTORC1 and AMPK can associate with ULK1 at the same time.
#  We assume that binding of AMPK at S758 shields S758 from phosphorylation.
#  We do not require MTORC1 to be bound to Rag-Ragulator complex to associate with ULK1.
#  We do not allow S792 or S855 in RAPTOR to be phosphorylated.
#  We assume that pS792 or pS855 inhibits RAPTOR interaction with substrates.
MTORC1(RNC,S792~0,S855~0)+ULK1(TOS)->MTORC1(RNC!1,S792~0,S855~0).ULK1(TOS!1) kf_MTORC1_ULK1
MTORC1(RNC!1).ULK1(TOS!1)->MTORC1(RNC)+ULK1(TOS) kr_MTORC1_ULK1
#  Phosphorylation of S758 does NOT induce dissociation of MTORC1 and ULK1.
#  MTOR catalytic activity requires association of MTORC1 with the Rag-Ragulator complex (ARM!+).
MTORC1(RNC!1,ARM!+).ULK1(TOS!1,S758~0)->MTORC1(RNC!1,ARM!+).ULK1(TOS!1,S758~P) kcat_MTORC1_ULK1

# phosphorylation of ULK1 by AMPK
#  requires that AMPK be Docked at S758 in ULK1
#  requires S758~0 in ULK1
#  Phosphorylation of the Docking site in AMPK (Dock~P) prevents AMPK-ULK1 association.
#  NB: both MTORC1 and AMPK can associate with ULK1 at the same time.
AMPK(Dock~0)+ULK1(S758~0)->AMPK(Dock~0!1).ULK1(S758~0!1) kf_AMPK_ULK1
AMPK(Dock~0!1).ULK1(S758~0!1)->AMPK(Dock~0)+ULK1(S758~0) kr_AMPK_ULK1
#  AMPK kinase activity depends on T-loop phosphorylation (T172~P)
#  We assume that binding of MTORC1 to ULK1 shields S317 from phosphorylation.
#  We assume that binding of MTORC1 to ULK1 does NOT shield S556 from phosphorylation.
#  Phosphorylation of S317/S556 does NOT induce dissociation of AMPK and ULK1.
#  We assume that AMPK substrates in ULK1 are phosphorylated slowly because they are situated
#  in sequences that are not good matches to the AMPK consensus sequence.
AMPK(T172~P,Dock~0!1).ULK1(S317~0,S758~0!1)->AMPK(T172~P,Dock~0!1).ULK1(S317~P,S758~0!1) 2*kcat_AMPK_ULK1
#AMPK(T172~P,Dock~0!1).ULK1(S317~P,S758~0!1)->AMPK(T172~P,Dock~0!1).ULK1(S317~PP,S758~0!1) 2*kcat_AMPK_ULK1
AMPK(T172~P,Dock~0!1).ULK1(S556~0,S758~0!1)->AMPK(T172~P,Dock~0!1).ULK1(S556~P,S758~0!1) kcat_AMPK_ULK1

# phosphorylation of (free/unDocked) AMPK by MTORC1-free ULK1 via Michaelis-Menten mechanism
#  negative feedback
#  We do not allow ULK1 to phosphorylate AMPK's docking site when AMPK is bound
#   to ULK1, assuming that the docking site is shielded in this scenario.
#  We assume that ULK1's kinase domain is not free to interact with substrates unless S317~P is true.
ULK1(kin,S317~P)+AMPK(Dock~0)->ULK1(kin!1,S317~P).AMPK(Dock~0!1) kp1_ULK1_AMPK
ULK1(kin!1).AMPK(Dock~0!1)->ULK1(kin)+AMPK(Dock~0) km1_ULK1_AMPK
#  kinase activity requires phosphorylation of S317 
ULK1(kin!1,S317~P).AMPK(Dock~0!1)->ULK1(kin,S317~P)+AMPK(Dock~P) kcat_ULK1_AMPK

# phosphorylation of ULK1-free MTORC1 by MTORC1-free ULK1 via Michaelis-Menten mechanism
ULK1(kin,S317~P)+MTORC1(S855~0)->ULK1(kin!1,S317~P).MTORC1(S855~0!1) kp1_ULK1_MTORC1
ULK1(kin!1).MTORC1(S855~0!1)->ULK1(kin)+MTORC1(S855~0) km1_ULK1_MTORC1
ULK1(kin!1,S317~P).MTORC1(S855~0!1)->ULK1(kin,S317~P)+MTORC1(S855~P) kcat_ULK1_MTORC1

ULK1(kin,S317~P)+MTORC1(S792~0)->ULK1(kin!1,S317~P).MTORC1(S792~0!1) kp1_ULK1_MTORC1
ULK1(kin!1).MTORC1(S792~0!1)->ULK1(kin)+MTORC1(S792~0) km1_ULK1_MTORC1
ULK1(kin!1,S317~P).MTORC1(S792~0!1)->ULK1(kin,S317~P)+MTORC1(S792~P) kcat_ULK1_MTORC1

ULK1(kin,S317~P)+MTORC1(S859~0)->ULK1(kin!1,S317~P).MTORC1(S859~0!1) kp1_ULK1_MTORC1
ULK1(kin!1).MTORC1(S859~0!1)->ULK1(kin)+MTORC1(S859~0) km1_ULK1_MTORC1
ULK1(kin!1,S317~P).MTORC1(S859~0!1)->ULK1(kin,S317~P)+MTORC1(S859~P) kcat_ULK1_MTORC1

# fast dissociation if all of S792, S855, S859 are phosphorylated
ULK1(TOS!1).MTORC1(RNC!1,S792~P,S855~P,S859~P)->ULK1(TOS)+MTORC1(RNC,S792~P,S855~P,S859~P) 1e8

# phosphorylation of EIF4EBP1 by MTORC1
#  We do not require that MTORC1 be bound to Rag-Ragulator complex to interact with substrates.
MTORC1(RNC,S792~0,S855~0)+EIF4EBP1(S65_T70~0)->MTORC1(RNC!1,S792~0,S855~0).EIF4EBP1(S65_T70~0!1) kf_MTORC1_EIF4EBP1
MTORC1(RNC!1).EIF4EBP1(S65_T70~0!1)->MTORC1(RNC)+EIF4EBP1(S65_T70~0) kr_MTORC1_EIF4EBP1
#  We assume that MTORC1's kinase activity is dependent on Docking to a Rag-Ragulator complex.
#  We require S855~0 in RAPTOR.
MTORC1(RNC!1,ARM!+).EIF4EBP1(S65_T70~0!1)->MTORC1(RNC,ARM!+)+EIF4EBP1(S65_T70~P) kcat_MTORC1_EIF4EBP1

# phosphorylation of AMBRA1 by ULK1
#  We assume that ULK1's kinase domain is only free to interact with substrates when S317~P
ULK1(kin,S317~P)+AMBRA1(S465_S635~0)->ULK1(kin!1,S317~P).AMBRA1(S465_S635~0!1) kp1_ULK1_AMBRA1
ULK1(kin!1).AMBRA1(S465_S635~0!1)->ULK1(kin)+AMBRA1(S465_S635~0) km1_ULK1_AMBRA1
#  We assume that ULK1 kinase activity depends on pS317
ULK1(kin!1,S317~P).AMBRA1(S465_S635~0!1)->ULK1(kin,S317~P)+AMBRA1(S465_S635~P) kcat_ULK1_AMBRA1

# phosphorylation of MTORC1 by AMPK via Michaelis-Menten mechanism
#  We assume that phosphorylation of AMPK's Docking site is also accompanied 
#  by inhibitory phosphorylation that deactivates AMPK kinase activity.
#  We assume that the T172~0 form of AMPK is not allowed to interact with substrates.
AMPK(kin,T172~P,Dock~0)+MTORC1(ARM,S792~0)->AMPK(kin!1,T172~P,Dock~0).MTORC1(ARM,S792~0!1) kp1_AMPK_MTORC1
AMPK(kin!1).MTORC1(S792~0!1)->AMPK(kin)+MTORC1(S792~0) km1_AMPK_MTORC1
AMPK(kin!1,T172~P,Dock~0).MTORC1(S792~0!1)->AMPK(kin,T172~P,Dock~0)+MTORC1(S792~P) kcat_AMPK_MTORC1

# dephosphorylation of AMPK
AMPK(Dock~P)->AMPK(Dock~0) k_dephos_AMPK_pDock

# dephosphorylation of MTORC1
MTORC1(S792~P)->MTORC1(S792~0) k_dephos_MTORC1_pS792 # implicit shielding from phosphatases by 14-3-3 proteins
MTORC1(S855~P)->MTORC1(S855~0) k_dephos_MTORC1_pS855
MTORC1(S859~P)->MTORC1(S859~0) k_dephos_MTORC1_pS859


# dephosphorylation of ULK1
ULK1(S317~P)->ULK1(S317~0) k_dephos_ULK1_pS317
ULK1(S556~P)->ULK1(S556~0) k_dephos_ULK1_pS758 # implicit shielding from phosphatases by 14-3-3 proteins
ULK1(S758~P)->ULK1(S758~0) k_dephos_ULK1_pS758

# dephosphorylation of EIF4EBP1
EIF4EBP1(S65_T70~P)->EIF4EBP1(S65_T70~0) k_dephos_pEIF4EBP1

# dephosphorylaition of AMBRA1
AMBRA1(S465_S635~P)->AMBRA1(S465_S635~0) k_dephos_pAMBRA1

end reaction rules

end model

begin actions

# low-stress condition
setParameter("k_energy_stress",1)
setParameter("k_nutrient_stress",1)
simulate({suffix=>"equilibrate",method=>"nf",complex=>1,gml=>10000000,get_final_state=>1,\
t_start=>0,seed=>13579,t_end=>20,n_steps=>20})
writeModel({suffix=>"equilibrated"})

# simulate induction of stress
resetConcentrations()
setParameter("k_energy_stress",1)
setParameter("k_nutrient_stress",300)
simulate({suffix=>"nutritionStress",method=>"nf",complex=>1,gml=>10000000,get_final_state=>1,\
t_start=>0,t_end=>3600,n_steps=>500})

resetConcentrations()
setParameter("k_energy_stress",300)
setParameter("k_nutrient_stress",1)
simulate({suffix=>"energyStress",method=>"nf",complex=>1,gml=>10000000,get_final_state=>1,\
t_start=>0,t_end=>3600,n_steps=>500})

#setParameter("k_energy_stress",1)
#resetConcentrations()
#parameter_scan({suffix=>"energyStress",method=>"nf",t_start=>0,t_end=>3600,n_steps=>90,\
#                parameter=>"k_nutrient_stress",par_min=>1,par_max=>300,n_scan_pts=>15,log_scale=>1})

#setParameter("k_nutrient_stress",1)
#resetConcentrations()
#parameter_scan({suffix=>"nutritientStress",method=>"nf",t_start=>0,t_end=>3600,n_steps=>90,\
#                parameter=>"k_energy_stress",par_min=>1,par_max=>300,n_scan_pts=>15,log_scale=>1})
	


end actions
